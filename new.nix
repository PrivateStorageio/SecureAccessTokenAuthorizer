let
  sources = import nix/sources.nix;
in
{ pkgs ? import sources.release2015 {}
, pypiData ? sources.pypi-deps-db
, mach-nix ? import sources.mach-nix { inherit pkgs pypiData; }
, tahoe-lafs-repo ? sources.tahoe-lafs
}:
  let
    providers = {
      _default = "sdist,nixpkgs,wheel";
      tahoe-lafs = "nixpkgs";
      # not packaged in nixpkgs at all, we can use the binary wheel from
      # pypi though.
      python-challenge-bypass-ristretto = "wheel";
      # Pure python packages that don't build correctly from sdists
      # - patches in nixpkgs that don't apply
      boltons = "wheel";
      chardet = "wheel";
      urllib3 = "wheel";
      # - incorrectly detected dependencies due to pbr
      fixtures = "wheel";
      testtools = "wheel";
      traceback2 = "wheel";
    };
    tahoe-lafs = mach-nix.buildPythonPackage {
      python = "python27";
      name = "tahoe-lafs";
      version = "1.16.post999";
      inherit providers;
      # See https://github.com/DavHau/mach-nix/issues/190
      requirementsExtra = ''
        pyrsistent < 0.17
        foolscap == 0.13.1
        configparser
        eliot
        '';
      postPatch = ''
        cat > src/allmydata/_version.py <<EOF
        # This _version.py is generated by nix.

        verstr = "1.16.post999+git-${tahoe-lafs-repo.rev}"
        __version__ = verstr
        EOF
      '';
      src = tahoe-lafs-repo;
    };
  in
    mach-nix.buildPythonApplication rec {
      python = "python27";
      name = "zero-knowledge-access-pass-authorizer";
      src = ./.;
      inherit providers;
      # We need to specify requirements here, since mach-nix does not
      # grab reqs from nixpkg dependencies
      # TODO: File issue
      requirementsExtra = tahoe-lafs.requirements;
      # Add 
      overridesPre = [
        (
          self: super: {
            inherit tahoe-lafs;
          }
        )
      ];
      format = "setuptools";
      # Record some settings here, so downstream nix files can consume them.
      meta.mach-nix = { inherit python providers; };
    }
